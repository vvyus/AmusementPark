
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	АдресФото = ПолучитьНавигационнуюСсылку(Объект.Ссылка, "Фото");

КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Если ЭтоАдресВременногоХранилища(АдресФото) Тогда
		ТекущийОбъект.Фото = Новый ХранилищеЗначения(ПолучитьИзВременногоХранилища(АдресФото));
	КонецЕсли;	

КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	Если ЭтоАдресВременногоХранилища(АдресФото) Тогда
		ПоместитьВоВременноеХранилище(Неопределено, АдресФото);
		АдресФото = ПолучитьНавигационнуюСсылку(Объект.Ссылка, "Фото");
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура АдресФотоНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ВыратьФото();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОчиститьФото(Команда)
	
	Если ЭтоАдресВременногоХранилища(АдресФото) Тогда
		ПоместитьВоВременноеХранилище(Неопределено, АдресФото);
	КонецЕсли;
		
	АдресФото = ПоместитьВоВременноеХранилище(Неопределено, УникальныйИдентификатор);	
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Асинх Процедура ВыратьФото()
	
	ПараметрыДиалога = Новый ПараметрыДиалогаПомещенияФайлов;
	ПараметрыДиалога.Фильтр = НСтр("ru = 'Картинка|*.jpg;*.png'");
	
	РезультатПомещения = Ждать ПоместитьФайлНаСерверАсинх(,,, ПараметрыДиалога, УникальныйИдентификатор);
	
	Если РезультатПомещения = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если РезультатПомещения.ПомещениеФайлаОтменено Тогда
		Возврат;
	КонецЕсли;
	
	АдресФото = РезультатПомещения.Адрес;
	
КонецПроцедуры

#КонецОбласти
