
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	СформироватьДиаграммы();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПериодПриИзменении(Элемент)
	СформироватьДиаграммы();
КонецПроцедуры

&НаКлиенте
Процедура ПосещенияАттракционовВыбор(Элемент, ЗначениеДиаграммы, СтандартнаяОбработка)
	
	Аттракцион = ЗначениеДиаграммы.Серия.Значение;
	День = ЗначениеДиаграммы.Точка.Значение;
	
	ПользовательскиеНастройки = НастройкиОтчетаДетализацияПосещений(Аттракцион, День);
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ПользовательскиеНастройки", ПользовательскиеНастройки);
	ПараметрыФормы.Вставить("СформироватьПриОткрытии", Истина);
	
	ОткрытьФорму("Отчет.ДетализацияПосещений.ФормаОбъекта", ПараметрыФормы);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Обновить(Команда)
	СформироватьДиаграммы();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура СформироватьДиаграммы()
	
	СформироватьДиаграммуПосещенияАттракционов();
	СформироватьДиаграммуРаспределениеАттракционов();
	
	ВремяПоследнегоОбновления = ТекущаяДатаСеанса();
	
КонецПроцедуры

&НаСервере
Процедура СформироватьДиаграммуПосещенияАттракционов()
	
	ПосещенияАттракционов.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ПосещенияАттракционовОбороты.Период КАК День,
		|	ПосещенияАттракционовОбороты.Аттракцион,
		|	ПосещенияАттракционовОбороты.КоличествоОборот КАК КоличествоПосещений
		|ИЗ
		|	РегистрНакопления.ПосещенияАттракционов.Обороты(&НачалоПериода, &КонецПериода, День,) КАК ПосещенияАттракционовОбороты
		|
		|УПОРЯДОЧИТЬ ПО
		|	День";
	
	Запрос.УстановитьПараметр("НачалоПериода", Период.ДатаНачала);
	Запрос.УстановитьПараметр("КонецПериода", Период.ДатаОкончания);
		
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		Точка = ПосещенияАттракционов.УстановитьТочку(Выборка.День);
		Точка.Текст = Формат(Выборка.День, "ДЛФ=D;");
		Серия = ПосещенияАттракционов.УстановитьСерию(Выборка.Аттракцион);
		
		ПосещенияАттракционов.УстановитьЗначение(Точка, Серия, Выборка.КоличествоПосещений);
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура СформироватьДиаграммуРаспределениеАттракционов()
	
	РаспределениеАттракционов.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ПосещенияАттракционовОбороты.Аттракцион,
		|	ПосещенияАттракционовОбороты.КоличествоОборот КАК КоличествоПосещений,
		|	ПосещенияАттракционовОбороты.Аттракцион.ВидАттракциона КАК ВидАттракциона
		|ИЗ
		|	РегистрНакопления.ПосещенияАттракционов.Обороты(&НачалоПериода, &КонецПериода,,) КАК ПосещенияАттракционовОбороты
		|ИТОГИ
		|	СУММА(КоличествоПосещений)
		|ПО
		|	ВидАттракциона";
	
	Запрос.УстановитьПараметр("НачалоПериода", Период.ДатаНачала);
	Запрос.УстановитьПараметр("КонецПериода", Период.ДатаОкончания);
		
	ВыборкаВидыАттракционов = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ВыборкаВидыАттракционов.Следующий() Цикл
		
		Точка = РаспределениеАттракционов.УстановитьТочку("Виды аттракционов");		
		Серия = РаспределениеАттракционов.УстановитьСерию(ВыборкаВидыАттракционов.ВидАттракциона);
		
		РаспределениеАттракционов.УстановитьЗначение(Точка, Серия, ВыборкаВидыАттракционов.КоличествоПосещений);
		
		ВыборкаАттракционы = ВыборкаВидыАттракционов.Выбрать();
		
		Пока ВыборкаАттракционы.Следующий() Цикл
			
			Точка = РаспределениеАттракционов.УстановитьТочку("Аттракционы");		
			Серия = РаспределениеАттракционов.УстановитьСерию(ВыборкаАттракционы.Аттракцион);
			
			РаспределениеАттракционов.УстановитьЗначение(Точка, Серия, ВыборкаАттракционы.КоличествоПосещений);			
			
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция НастройкиОтчетаДетализацияПосещений(Аттракцион, День)
	
	Отчет = Отчеты.ДетализацияПосещений.Создать();
	Настройки = Отчет.КомпоновщикНастроек.Настройки;
	ПользовательскиеНастройки = Отчет.КомпоновщикНастроек.ПользовательскиеНастройки;
	
	// Установка параметра Период
	ПараметрПериод = Настройки.ПараметрыДанных.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("Период"));
	ПользовательскийПараметрПериод = 
		ПользовательскиеНастройки.Элементы.Найти(ПараметрПериод.ИдентификаторПользовательскойНастройки);
		
	ЗначениеПараметраПериод = Новый СтандартныйПериод;
	ЗначениеПараметраПериод.ДатаНачала = НачалоДня(День);
	ЗначениеПараметраПериод.ДатаОкончания = КонецДня(День);
	ПользовательскийПараметрПериод.Значение = ЗначениеПараметраПериод;
	
	// Установка отбора Аттракцион
	ПолеДляОтбора = Новый ПолеКомпоновкиДанных("Аттракцион");
	Для Каждого ЭлементОтбора Из Настройки.Отбор.Элементы Цикл
		Если ЭлементОтбора.ЛевоеЗначение = ПолеДляОтбора Тогда
			ПользовательскийПараметрАттракцион = 
				ПользовательскиеНастройки.Элементы.Найти(ЭлементОтбора.ИдентификаторПользовательскойНастройки);
			ПользовательскийПараметрАттракцион.Использование = Истина;
			ПользовательскийПараметрАттракцион.ПравоеЗначение = Аттракцион;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Возврат ПользовательскиеНастройки;	
	
КонецФункции

#КонецОбласти
